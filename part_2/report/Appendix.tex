
\section{Appendix}
\subsection{A1}
\begin{alltt}
dtmc

module client1
  state1 : [0..1] init 0; // State of the job (inactive/active)
  task1  : [0..5] init 0; // Length of the job
  
  // Create a new job - length chose non-deterministically
[create1] state1=0 -> 0.2 : (task1'=1) & (state1'=1) +
0.2 : (task1'=2) & (state1'=1) + 0.2 : (task1'=3) & (state1'=1) +
0.2 : (task1'=4) & (state1'=1) + 0.2 : (task1'=5) & (state1'=1);

  // Serve the job
  [serve1] state1=1 & task1>0 -> (task1'=task1-1);

  // Complete the job
  [finish1] state1=1 & task1=0 -> (state1'=0);

endmodule

module client2 = client1 [state1=state2,
                          task1=task2,
                          create1=create2,
                          serve1=serve2,
                          finish1=finish2 ]
endmodule

module scheduler
  job1 : [0..2] init 0; // First job in the queue
  job2 : [0..2] init 0; // Second job in the queue

  // Place a new job at the end of the queue
  [create1] job2=0 -> (job2'=1); 
  [create2] job2=0 -> (job2'=2);

  // Shift the queue if there is an empty slot
  [] job1=0 & job2>0 -> (job1'=job2) & (job2'=0);

  // Serve the job at the head of the queue
  [serve1] job1=1 -> true;
  [serve2] job1=2 -> true;

  // Complete the job at the head of the queue
  [finish1] job1=1 -> (job1'=0);
  [finish2] job1=2 -> (job1'=0);

endmodule

system
  scheduler || client1 || client2
endsystem
\end{alltt}

\subsection{B1.1}

\subsubsection{a,b}

\begin{alltt}
dtmc

const int timeMax = 1;

formula sum = (job1*tickets1+job2*tickets2+job3*tickets3);

module client1
  state1 : [0..1] init 0; // State of the job (inactive/active)
  task1  : [0..5] init 0; // Length of the job
  
  // Create a new job - length chose non-deterministically
  [create1] state1=0 -> 0.2 : (state1'=1) & (task1'=1) +
                        0.2 : (state1'=1) & (task1'=2) +
                        0.2 : (state1'=1) & (task1'=3) +
                        0.2 : (state1'=1) & (task1'=4) +
                        0.2 : (state1'=1) & (task1'=5);

  // Serve the job
  [serve1] state1=1 & task1>0 -> (task1'=task1-1);

  // Complete the job
  [finish1] state1=1 & task1=0 -> (state1'=0);

endmodule

module client2 = client1 [state1=state2,
                          task1=task2,
                          create1=create2,
                          serve1=serve2,
                          finish1=finish2 ]
endmodule

module client3 = client1 [state1=state3,
                          task1=task3,
                          create1=create3,
                          serve1=serve3,
                          finish1=finish3 ]
endmodule

module monitor 

  finished : bool init false;

  [finish1] true -> (finished' = true);

endmodule

module scheduler
  job1 : [0..1] init 0; // Is there a job from client1?
  job2 : [0..1] init 0; // Is there a job from client2?
  job3 : [0..1] init 0; // Is there a job from client3?

  tickets1 : [1..2] init 1;
  tickets2 : [1..2] init 1;
  tickets3 : [1..2] init 1;

  currentQueueIndex : [1..3] init 1; // The current queue index
  timeQuota : [0..timeMax] init 0; // The current time quota.

  // Record that there is a waiting job
  [create1] job1=0 -> (job1'=1) & (tickets1'=1);
  [create2] job2=0 -> (job2'=1) & (tickets2'=1);
  [create3] job3=0 -> (job3'=1) & (tickets3'=1);

  [] timeQuota<1 & (job1>0 | job2>0 | job3>0) ->
     job1*tickets1/sum : (currentQueueIndex'=1) & (timeQuota'=timeMax) +
     job2*tickets2/sum : (currentQueueIndex'=2) & (timeQuota'=timeMax) +
     job3*tickets3/sum : (currentQueueIndex'=3) & (timeQuota'=timeMax);

  // Serve the current job.
  [serve1] job1=1 & currentQueueIndex=1 & timeQuota>0 ->
           (timeQuota'=timeQuota-1);
  [serve2] job2=1 & currentQueueIndex=2 & timeQuota>0 ->
           (timeQuota'=timeQuota-1);
  [serve3] job3=1 & currentQueueIndex=3 & timeQuota>0 ->
           (timeQuota'=timeQuota-1);

  // Complete any job that has finished
  [finish1] job1=1 -> (job1'=0) & (timeQuota'=0);
  [finish2] job2=1 -> (job2'=0) & (timeQuota'=0);
  [finish3] job3=1 -> (job3'=0) & (timeQuota'=0);

endmodule

system
  scheduler || client1 || client2 || client3 || monitor
endsystem
\end{alltt}

\subsubsection{e}

\begin{alltt}dtmc

const int timeMax = 2;

formula sum = (job1*tickets1+job2*tickets2+job3*tickets3);

module client1
  state1 : [0..1] init 0; // State of the job (inactive/active)
  task1  : [0..5] init 0; // Length of the job
  tickets1 : [1..5] init 1;
  
  // Create a new job - length chose non-deterministically
  [create1] state1=0 -> 0.2 : (state1'=1) & (task1'=1) & (tickets1'=5) +
                        0.2 : (state1'=1) & (task1'=2) & (tickets1'=4) +
                        0.2 : (state1'=1) & (task1'=3) & (tickets1'=3) +
                        0.2 : (state1'=1) & (task1'=4) & (tickets1'=2) +
                        0.2 : (state1'=1) & (task1'=5) & (tickets1'=1);

  // Serve the job
  [serve1] state1=1 & task1>0 -> (task1'=task1-1);

  // Complete the job
  [finish1] state1=1 & task1=0 -> (state1'=0);

endmodule

module client2 = client1 [state1=state2,
                          task1=task2,
                          create1=create2,
                          serve1=serve2,
                          finish1=finish2,
                          tickets1=tickets2 ]
endmodule

module client3 = client1 [state1=state3,
                          task1=task3,
                          create1=create3,
                          serve1=serve3,
                          finish1=finish3,
                          tickets1=tickets3 ]
endmodule

module monitor 

  finished : bool init false;

  [finish1] true -> (finished' = true);

endmodule

module scheduler
  job1 : [0..1] init 0; // Is there a job from client1?
  job2 : [0..1] init 0; // Is there a job from client2?
  job3 : [0..1] init 0; // Is there a job from client3?

  currentQueueIndex : [1..3] init 1; // The current queue index
  timeQuota : [0..timeMax] init 0; // The current time quota.

  // Record that there is a waiting job
  [create1] job1=0 -> (job1'=1);// & (tickets1'=6-task1);
  [create2] job2=0 -> (job2'=1);// & (tickets2'=1);
  [create3] job3=0 -> (job3'=1);// & (tickets3'=1);

  [] timeQuota<1 & (job1>0 | job2>0 | job3>0) ->
     job1*tickets1/sum : (currentQueueIndex'=1) & (timeQuota'=timeMax) +
     job2*tickets2/sum : (currentQueueIndex'=2) & (timeQuota'=timeMax) +
     job3*tickets3/sum : (currentQueueIndex'=3) & (timeQuota'=timeMax);

  // Serve the current job.
  [serve1] job1=1 & currentQueueIndex=1 & timeQuota>0 -> (timeQuota'=timeQuota-1);
  [serve2] job2=1 & currentQueueIndex=2 & timeQuota>0 -> (timeQuota'=timeQuota-1);
  [serve3] job3=1 & currentQueueIndex=3 & timeQuota>0 -> (timeQuota'=timeQuota-1);

  // Complete any job that has finished
  [finish1] job1=1 -> (job1'=0) & (timeQuota'=0);
  [finish2] job2=1 -> (job2'=0) & (timeQuota'=0);
  [finish3] job3=1 -> (job3'=0) & (timeQuota'=0);

endmodule

system
  scheduler || client1 || client2 || client3 || monitor
endsystem
\end{alltt}

\subsection{B1.2}

\subsubsection{a}

\begin{alltt}
dtmc

const int timeMax = 1;

formula sum = (job1*(6-task1)+job2*(6-task2)+job3*(6-task3));

module client1
  state1 : [0..1] init 0; // State of the job (inactive/active)
  task1  : [0..5] init 0; // Length of the job
  
  // Create a new job - length chose non-deterministically
  [create1] state1=0 -> 0.2 : (state1'=1) & (task1'=1) +
                        0.2 : (state1'=1) & (task1'=2) +
                        0.2 : (state1'=1) & (task1'=3) +
                        0.2 : (state1'=1) & (task1'=4) +
                        0.2 : (state1'=1) & (task1'=5);

  // Serve the job
  [serve1] state1=1 & task1>0 -> (task1'=task1-1);

  // Complete the job
  [finish1] state1=1 & task1=0 -> (state1'=0);

endmodule

module client2 = client1 [state1=state2,
                          task1=task2,
                          create1=create2,
                          serve1=serve2,
                          finish1=finish2,
                          tickets1=tickets2 ]
endmodule

module client3 = client1 [state1=state3,
                          task1=task3,
                          create1=create3,
                          serve1=serve3,
                          finish1=finish3,
                          tickets1=tickets3 ]
endmodule

module monitor 

  finished : bool init false;

  [finish1] true -> (finished' = true);

endmodule

module scheduler
  job1 : [0..1] init 0; // Is there a job from client1?
  job2 : [0..1] init 0; // Is there a job from client2?
  job3 : [0..1] init 0; // Is there a job from client3?

  currentQueueIndex : [1..3] init 1; // The current queue index
  timeQuota : [0..timeMax] init 0; // The current time quota.

  // Record that there is a waiting job
  [create1] job1=0 -> (job1'=1);
  [create2] job2=0 -> (job2'=1);
  [create3] job3=0 -> (job3'=1);

  [] timeQuota<1 & (job1>0 | job2>0 | job3>0) ->
     job1*(6-task1)/sum : (currentQueueIndex'=1) & (timeQuota'=timeMax) +
     job2*(6-task2)/sum : (currentQueueIndex'=2) & (timeQuota'=timeMax) +
     job3*(6-task3)/sum : (currentQueueIndex'=3) & (timeQuota'=timeMax);

  // Serve the current job.
  [serve1] job1=1 & currentQueueIndex=1 & timeQuota>0 -> (timeQuota'=timeQuota-1);
  [serve2] job2=1 & currentQueueIndex=2 & timeQuota>0 -> (timeQuota'=timeQuota-1);
  [serve3] job3=1 & currentQueueIndex=3 & timeQuota>0 -> (timeQuota'=timeQuota-1);

  // Complete any job that has finished
  [finish1] job1=1 -> (job1'=0) & (timeQuota'=0);
  [finish2] job2=1 -> (job2'=0) & (timeQuota'=0);
  [finish3] job3=1 -> (job3'=0) & (timeQuota'=0);

endmodule

system
  scheduler || client1 || client2 || client3 || monitor
endsystem
\end{alltt}

\subsubsection{d}

\begin{alltt}
dtmc

const int timeMax = 1;

formula sum = (job1*(6-task1)+job2*(6-task2)+job3*(6-task3)+job4*(6-task4));

module client1
  state1 : [0..1] init 0; // State of the job (inactive/active)
  task1  : [0..5] init 0; // Length of the job
  
  // Create a new job - length chose non-deterministically
  [create1] state1=0 -> 1.0 : (state1'=1) & (task1'=1);

  // Serve the job
  [serve1] state1=1 & task1>0 -> (task1'=task1-1);

  // Complete the job
  [finish1] state1=1 & task1=0 -> (state1'=0);

endmodule

module client2
  state2 : [0..1] init 0; // State of the job (inactive/active)
  task2  : [0..5] init 0; // Length of the job
  
  // Create a new job - length chose non-deterministically
  [create2] state2=0 -> 1.0 : (state2'=1) & (task2'=5);

  // Serve the job
  [serve2] state2=1 & task2>0 -> (task2'=task2-1);

  // Complete the job
  [finish2] state2=1 & task2=0 -> (state2'=0);

endmodule

module client3 = client1 [state1=state3,
                          task1=task3,
                          create1=create3,
                          serve1=serve3,
                          finish1=finish3,
                          tickets1=tickets3 ]
endmodule

module client4 = client1 [state1=state4,
                          task1=task4,
                          create1=create4,
                          serve1=serve4,
                          finish1=finish4,
                          tickets1=tickets4 ]
endmodule

module monitor 

  finished : bool init false;

  [finish2] true -> (finished' = true);

endmodule

module scheduler
  job1 : [0..1] init 0; // Is there a job from client1?
  job2 : [0..1] init 0; // Is there a job from client2?
  job3 : [0..1] init 0; // Is there a job from client3?
  job4 : [0..1] init 0; // Is there a job from client4?

  currentQueueIndex : [1..4] init 1; // The current queue index
  timeQuota : [0..timeMax] init 0; // The current time quota.

  // Record that there is a waiting job
  [create1] job1=0 -> (job1'=1);
  [create2] job2=0 -> (job2'=1);
  [create3] job3=0 -> (job3'=1);
  [create4] job4=0 -> (job4'=1);

  [] timeQuota<1 & (job1>0 | job2>0 | job3>0 | job4>0) ->
     job1*(6-task1)/sum : (currentQueueIndex'=1) & (timeQuota'=timeMax) +
     job2*(6-task2)/sum : (currentQueueIndex'=2) & (timeQuota'=timeMax) +
     job3*(6-task3)/sum : (currentQueueIndex'=3) & (timeQuota'=timeMax) +
     job4*(6-task4)/sum : (currentQueueIndex'=4) & (timeQuota'=timeMax);

  // Serve the current job.
  [serve1] job1=1 & currentQueueIndex=1 & timeQuota>0 -> (timeQuota'=timeQuota-1);
  [serve2] job2=1 & currentQueueIndex=2 & timeQuota>0 -> (timeQuota'=timeQuota-1);
  [serve3] job3=1 & currentQueueIndex=3 & timeQuota>0 -> (timeQuota'=timeQuota-1);
  [serve4] job4=1 & currentQueueIndex=4 & timeQuota>0 -> (timeQuota'=timeQuota-1);

  // Complete any job that has finished
  [finish1] job1=1 -> (job1'=0) & (timeQuota'=0);
  [finish2] job2=1 -> (job2'=0) & (timeQuota'=0);
  [finish3] job3=1 -> (job3'=0) & (timeQuota'=0);
  [finish4] job4=1 -> (job4'=0) & (timeQuota'=0);

endmodule

system
  scheduler || client1 || client2 || client3 || client4 || monitor
endsystem
\end{alltt}

\subsection{C1}

\subsubsection{Equal probability, simple rewards model}

\begin{alltt}
dtmc

formula sum1 = (prio1*job1*(task1>0?1:0) +
                prio2*job2*(task2>0?1:0) +
                prio3*job3*(task3>0?1:0));
formula sum2 = (prio1*job1*(task1>0?1:0)*(serv1=1?0:1) +
                prio2*job2*(task2>0?1:0)*(serv1=2?0:1) +
                prio3*job3*(task3>0?1:0)*(serv1=3?0:1));

rewards "waiting_time"
  [schedule1] state1=1 & task1>0 : 1;
  [schedule1] state2=1 & task2>0 : 1;
  [schedule1] state3=1 & task3>0 : 1;
endrewards

module client1
  state1 : [0..1] init 0; // State of the job (inactive/active)
  task1  : [0..5] init 0; // Length of the job
  
  // Create a new job - length chose non-deterministically
  [create1] state1=0 -> 0.2 : (state1'=1) & (task1'=1) +
                        0.2 : (state1'=1) & (task1'=2) +
                        0.2 : (state1'=1) & (task1'=3) +
                        0.2 : (state1'=1) & (task1'=4) +
                        0.2 : (state1'=1) & (task1'=5);

  // Serve the job
  [serve1] state1=1 & task1>0 -> (task1'=task1-1);

  // Complete the job
  [finish1] state1=1 & task1=0 -> (state1'=0);

endmodule

module client2 = client1 [state1=state2,
                          task1=task2,
                          create1=create2,
                          serve1=serve2,
                          finish1=finish2 ]
endmodule

module client3 = client1 [state1=state3,
                          task1=task3,
                          create1=create3,
                          serve1=serve3,
                          finish1=finish3 ]
endmodule

module priority1

  prio1 : [1..2] init 1;

  [create1] true -> 0.5 : (prio1'=1) + 0.5 : (prio1'=2);

endmodule

module priority2 = priority1 [prio1=prio2, create1=create2] endmodule

module priority3 = priority1 [prio1=prio3, create1=create3] endmodule

module server1

  serv1 : [0..3] init 0;
  ready1 : bool init false;

  [schedule1] sum1>0 -> 
    (prio1*job1*(task1>0?1:0))/sum1 : (serv1'=1) & (ready1'=true) +
    (prio2*job2*(task2>0?1:0))/sum1 : (serv1'=2) & (ready1'=true) +
    (prio3*job3*(task3>0?1:0))/sum1 : (serv1'=3) & (ready1'=true);

  [serve1] serv1=1 & ready1 -> (serv1'=0) & (ready1'=false);
  [serve1] serv2=1 & ready2 -> true;
  [serve2] serv1=2 & ready1 -> (serv1'=0) & (ready1'=false);
  [serve2] serv2=2 & ready2 -> true;
  [serve3] serv1=3 & ready1 -> (serv1'=0) & (ready1'=false);
  [serve3] serv2=3 & ready2 -> true;

endmodule

module server2

  serv2 : [0..3] init 0;
  ready2 : bool init false;

  [schedule2] sum2>0 ->
    (prio1*job1*(task1>0?1:0)*(serv1=1?0:1))/sum2 : (serv2'=1) & (ready2'=true) +
    (prio2*job2*(task2>0?1:0)*(serv1=2?0:1))/sum2 : (serv2'=2) & (ready2'=true) +
    (prio3*job3*(task3>0?1:0)*(serv1=3?0:1))/sum2 : (serv2'=3) & (ready2'=true);
  [schedule2] sum2=0 -> (ready2'=false);

  [serve1] serv2=1 & ready2 -> (serv2'=0) & (ready2'=false);
  [serve1] serv1=1 & ready1 -> true;
  [serve2] serv2=2 & ready2 -> (serv2'=0) & (ready2'=false);
  [serve2] serv1=2 & ready1 -> true;
  [serve3] serv2=3 & ready2 -> (serv2'=0) & (ready2'=false);
  [serve3] serv1=3 & ready1 -> true;

endmodule

module scheduler
  job1 : [0..1] init 0; // Is there a job from client1?
  job2 : [0..1] init 0; // Is there a job from client2?
  job3 : [0..1] init 0; // Is there a job from client3?

  step : [0..2] init 0;

  // Record that there is a waiting job
  [create1] job1=0 & step=0 -> (job1'=1);
  [create2] job2=0 & step=0 -> (job2'=1);
  [create3] job3=0 & step=0 -> (job3'=1);

  // Only schedule jobs that are in the queue and that have non-zero lengths.
  [schedule1] step=0 -> (step'=1);
  [schedule2] step=1 -> (step'=2);

  // Serve a job.
  [serve1] job1=1 & step=2 -> true;
  [serve2] job2=1 & step=2 -> true;
  [serve3] job3=1 & step=2 -> true;

  [] step=2 & !ready1 & !ready2 -> (step'=0);

  // Complete any job that has finished
  [finish1] job1=1 & step=0 -> (job1'=0);
  [finish2] job2=1 & step=0 -> (job2'=0);
  [finish3] job3=1 & step=0 -> (job3'=0);

endmodule

system
  scheduler || client1 || client2 || client3 ||
  server1 || server2 || priority1 || priority2 || priority3
endsystem
\end{alltt}

\subsubsection{Priority-reserved server, simple rewards model}

\begin{alltt}dtmc
formula eli_1 = (job1*(task1>0?1:0)>0);
formula eli_2 = (job2*(task2>0?1:0)>0);
formula eli_3 = (job3*(task3>0?1:0)>0);
formula m_1 = (eli_1 & (!eli_2|prio1>=prio2) & (!eli_3|prio1>=prio3)?prio1:0);
formula m_2 = (eli_2 & (!eli_1|prio2>=prio1) & (!eli_3|prio2>=prio3)?prio2:0);
formula m_3 = (eli_3 & (!eli_1|prio3>=prio1) & (!eli_2|prio3>=prio2)?prio3:0);
formula sum1_priority = (m_1 + m_2 + m_3);
formula sum2 = (prio1*job1*(task1>0?1:0)*(serv1=1?0:1) +
                prio2*job2*(task2>0?1:0)*(serv1=2?0:1) +
                prio3*job3*(task3>0?1:0)*(serv1=3?0:1));

rewards "waiting_time"
  [schedule1] state1=1 & task1>0 : 1;
  [schedule1] state2=1 & task2>0 : 1;
  [schedule1] state3=1 & task3>0 : 1;
endrewards

module client1
  state1 : [0..1] init 0; // State of the job (inactive/active)
  task1  : [0..5] init 0; // Length of the job
  
  // Create a new job - length chose non-deterministically
  [create1] state1=0 -> 0.2 : (state1'=1) & (task1'=1) +
                        0.2 : (state1'=1) & (task1'=2) +
                        0.2 : (state1'=1) & (task1'=3) +
                        0.2 : (state1'=1) & (task1'=4) +
                        0.2 : (state1'=1) & (task1'=5);

  // Serve the job
  [serve1] state1=1 & task1>0 -> (task1'=task1-1);

  // Complete the job
  [finish1] state1=1 & task1=0 -> (state1'=0);

endmodule

module client2 = client1 [state1=state2,
                          task1=task2,
                          create1=create2,
                          serve1=serve2,
                          finish1=finish2 ]
endmodule

module client3 = client1 [state1=state3,
                          task1=task3,
                          create1=create3,
                          serve1=serve3,
                          finish1=finish3 ]
endmodule

module priority1

  prio1 : [1..2] init 1;

  [create1] true -> 0.5 : (prio1'=1) + 0.5 : (prio1'=2);

endmodule

module priority2 = priority1 [prio1=prio2, create1=create2] endmodule

module priority3 = priority1 [prio1=prio3, create1=create3] endmodule

module server1

  serv1 : [0..3] init 0;
  ready1 : bool init false;

  [schedule1] sum1_priority>0 ->
    m_1/sum1_priority : (serv1'=1) & (ready1'=true) +
    m_2/sum1_priority : (serv1'=2) & (ready1'=true) +
    m_3/sum1_priority : (serv1'=3) & (ready1'=true);

  [serve1] serv1=1 & ready1 -> (serv1'=0) & (ready1'=false);
  [serve1] serv2=1 & ready2 -> true;
  [serve2] serv1=2 & ready1 -> (serv1'=0) & (ready1'=false);
  [serve2] serv2=2 & ready2 -> true;
  [serve3] serv1=3 & ready1 -> (serv1'=0) & (ready1'=false);
  [serve3] serv2=3 & ready2 -> true;

endmodule

module server2

  serv2 : [0..3] init 0;
  ready2 : bool init false;

  [schedule2] sum2>0 ->
    (prio1*job1*(task1>0?1:0)*(serv1=1?0:1))/sum2 : (serv2'=1) & (ready2'=true) +
    (prio2*job2*(task2>0?1:0)*(serv1=2?0:1))/sum2 : (serv2'=2) & (ready2'=true) +
    (prio3*job3*(task3>0?1:0)*(serv1=3?0:1))/sum2 : (serv2'=3) & (ready2'=true);
  [schedule2] sum2=0 -> (ready2'=false);

  [serve1] serv2=1 & ready2 -> (serv2'=0) & (ready2'=false);
  [serve1] serv1=1 & ready1 -> true;
  [serve2] serv2=2 & ready2 -> (serv2'=0) & (ready2'=false);
  [serve2] serv1=2 & ready1 -> true;
  [serve3] serv2=3 & ready2 -> (serv2'=0) & (ready2'=false);
  [serve3] serv1=3 & ready1 -> true;

endmodule

module scheduler
  job1 : [0..1] init 0; // Is there a job from client1?
  job2 : [0..1] init 0; // Is there a job from client2?
  job3 : [0..1] init 0; // Is there a job from client3?

  step : [0..2] init 0;

  // Record that there is a waiting job
  [create1] job1=0 & step=0 -> (job1'=1);
  [create2] job2=0 & step=0 -> (job2'=1);
  [create3] job3=0 & step=0 -> (job3'=1);

  // Only schedule jobs that are in the queue and that have non-zero lengths.
  [schedule1] step=0 -> (step'=1);
  [schedule2] step=1 -> (step'=2);

  // Serve a job.
  [serve1] job1=1 & step=2 -> true;
  [serve2] job2=1 & step=2 -> true;
  [serve3] job3=1 & step=2 -> true;

  [] step=2 & !ready1 & !ready2 -> (step'=0);

  // Complete any job that has finished
  [finish1] job1=1 & step=0 -> (job1'=0);
  [finish2] job2=1 & step=0 -> (job2'=0);
  [finish3] job3=1 & step=0 -> (job3'=0);

endmodule

system
  scheduler || client1 || client2 || client3 ||
  server1 || server2 || priority1 || priority2 || priority3
endsystem
\end{alltt}

